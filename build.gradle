apply plugin: 'android'
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://maven.sms.co.at/content/repositories/thirdparty"
        url "http://maven.sms.co.at/content/repositories/releases"
    }
}

android {
    compileSdkVersion 17
    buildToolsVersion '17'

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 17
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
        }

        instrumentTest.setRoot('tests')
    }
    signingConfigs {
        release
    }
    
    if (project.hasProperty('buildNumber')) defaultConfig.versionName += '-' + buildNumber
  
    if (project.hasProperty('storeFile')) {
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }

    flavorGroups "target-app", "store"

    productFlavors {
        tablet {
            flavorGroup "target-app"
            packageName "com.mysms.android.tablet.theme.dark"
            versionName '1.5-SNAPSHOT'
            versionCode 7
        }

        mirror {
            flavorGroup "target-app"
            packageName "com.mysms.android.mirror.theme.dark"
            versionName '1.1-SNAPSHOT'
            versionCode 3
        }

        play { flavorGroup "store" }
        amazon { flavorGroup "store" }
    }

    dependencies {
        compile 'com.mysms.android:mysms-android-tablet-theme-base:1.3'
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        applicationVariants.all { variant ->
            def regex = "^.*(tablet|mirror)(play|amazon).*\$";
            def apk = variant.packageApplication.outputFile;
            def app = variant.baseName.replaceAll(regex, "\$1");
            def store = variant.baseName.replaceAll(regex, "\$2");

            // update screen definitions after merging the manifest
            variant.processManifest.doLast {
                def screensFile = "src/${app}/screens.xml"
                try {
                    // get screens from file
                    def screens = file(screensFile).text

                    // overwrite screen definitions in a copy of the already merged manifest
                    copy {
                        from("${buildDir}/manifests") {
                            include "${variant.dirName}/AndroidManifest.xml"
                        }
                        into("${buildDir}/filtered_manifests")
                        filter { line -> line.replaceAll("<supports-screens(.*)/>", screens) }
                    }

                    // use the new manifest from now on
                    variant.processResources.manifestFile = file("${buildDir}/filtered_manifests/${variant.dirName}/AndroidManifest.xml")
                } catch (e){
                    // file does not exist ... keep default screen definitions
                }
			}

            def newName = "mysms-" + app + "-theme-dark-" + store + (apk.name.contains("-unaligned") ? "-unaligned" : "") + "-" + variant.mergedFlavor.versionName + ".apk";

            if (variant.zipAlign) {
                variant.packageApplication.outputFile = new File(apk.parentFile, newName);
                variant.zipAlign.outputFile = new File(apk.parentFile, newName.replace("-unaligned", ""));
            } else {
                variant.packageApplication.outputFile = new File(apk.parentFile, newName.replace("-unaligned", ""));
            }
        }
    }
}
 
tasks.withType(Compile) {
    options.encoding = "UTF-8"
}
